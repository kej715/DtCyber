NJMLIB
      SUBROUTINE NJMOPEN(HDR,ACN,RC,ANAME,NC)
C
C     NJMOPEN - OPEN A CONNECTION WITH NJF
C
C       HDR   - (IN)  NETWORK COMMUNICATION HEADER ARRAY
C       ACN   - (OUT) APPLICATION CONNECTION NUMBER
C       RC    - (OUT) RETURN CODE (0 INDICATES SUCCESS)
C       ANAME - (IN)  NETWORK APPLICATION NAME
C       NC    - (IN)  MAXIMUM NUMBER OF CONNECTIONS SUPPORTED
C
      IMPLICIT INTEGER (A-Z)
      DIMENSION HDR(10),TA(20)

      HDR(1) = (ANAME.AND.MASK(42)).OR.SHIFT(1,12).OR.NC
      HDR(4) = 1
      HDR(5) = SHIFT(400,48)
C
C     ESTABLISH COMMUNICATION WITH NAM
C
      CALL QTOPEN(HDR)
      RC = SHIFT(HDR(5),-12).AND.(O"77")
      IF (RC.NE.0) RETURN
C
C     CONNECT TO NJF
C
      HDR(7) = MASK(42).AND.7HNJF
      CALL QTLINK

 10   CONTINUE
      CALL NJMPOLL(HDR,ACN,RC,10,TA,20)
      IF (RC.EQ.1.OR.RC.EQ.0) THEN
        GO TO 10
      ELSE IF (RC.EQ.14) THEN
        RC = 0
      ENDIF

      END
      SUBROUTINE NJMPOLL(HDR,ACN,RC,SLEEP,TA,TAL)
C
C     NJMPOLL - POLL THE NETWORK FOR ACTIVITY
C
C       HDR   - (IN)  NETWORK COMMUNICATION HEADER ARRAY
C       ACN   - (OUT) APPLICATION CONNECTION NUMBER
C       RC    - (OUT) RETURN CODE
C                       0 INDICATES SUCCESS
C                       1 INDICATES NO ACTIVITY
C                       6 INDICATES CONNECTION ENDED
C                      13 INDICATES CONNECTION REJECTED
C                      14 INDICATES CONNECTION ACCEPTED
C       SLEEP - (IN)  MAXIMUM SECONDS TO WAIT FOR ACTIVITY
C       TA    - (OUT) TEXT AREA TO RECEIVE DATA
C       TAL   - (OUT) NUMBER OF WORDS RECEIVED
C
      IMPLICIT INTEGER (A-Z)
      DIMENSION HDR(10),TA(*)

      HDR(5) = SHIFT(TAL,48).OR.SHIFT(SLEEP,30)
      CALL QTGET(TA)
      CALL NJMSTAT(HDR,ACN,RC)

      END
      SUBROUTINE NJMRECV(TA,MTYPE,FUSER,FNODE,TUSER,TEXT,TEXTL,RC)
C
C     NJMRECV - RECEIVE AND DECODE A NETWORK MESSAGE
C
C       TA    - (IN)  TEXT AREA CONTAINING RAW RECEIVED DATA
C       MTYPE - (OUT) MESSAGE TYPE
C                      'M' INDICATES MESSAGE
C                      'S' INDICATES STATUS
C       FUSER - (OUT) NAME OF ORIGIN USER OR SERVICE
C       FNODE - (OUT) NAME OF ORIGIN NODE
C       TUSER - (OUT) NAME OF DESTINATION USER OR SERVICE
C       TEXT  - (OUT) TEXT OF RECEIVED MESSAGE
C       TEXTL - (OUT) LENGTH OF TEXT (NUMBER OF CHARACTERS)
C       RC    - (OUT) RETURN CODE ASSOCIATED WITH STATUS MESSAGE
C                       0 INDICATES SUCCESS
C                       1 INDICATES BAD REQUEST
C                       2 INDICATES RESOURCE EXHAUSTION
C
C$    COLLATE(FIXED)
      IMPLICIT INTEGER (A-Z)
      DIMENSION TA(*)
      CHARACTER*1 MTYPE
      CHARACTER*8 FNODE,FUSER,TUSER
      CHARACTER TEXT*(*)

      MTYPE = CHAR(SHIFT(TA(1),6).AND.-MASK(54))
      IF (MTYPE.EQ.'S') THEN
        RC = TA(1).AND.-MASK(6)
      ELSEIF (MTYPE.EQ.'M') THEN
        RC = 0
        VAL = SHIFT(TA(1),6)
        DO 10 I = 1,8
          VAL = SHIFT(VAL,6)
          FUSER(I:I) = CHAR(VAL.AND.-MASK(54))
 10     CONTINUE
        VAL = TA(2)
        DO 20 I = 1,8
          VAL = SHIFT(VAL,6)
          TUSER(I:I) = CHAR(VAL.AND.-MASK(54))
 20     CONTINUE
        VAL = TA(3)
        DO 30 I = 1,8
          VAL = SHIFT(VAL,6)
          FNODE(I:I) = CHAR(VAL.AND.-MASK(54))
 30     CONTINUE
        TEXTL  = TA(3).AND.-MASK(48)
        WORDS = (TEXTL + 9) / 10
        CI    = 1
        DO 50 W = 1,WORDS
          WORD = TA(W + 3)
          DO 40 I = 1,10
            WORD = SHIFT(WORD,6)
            TEXT(CI:CI) = CHAR(WORD.AND.-MASK(54))
            CI = CI + 1
 40       CONTINUE
 50     CONTINUE
      ELSE
        CALL REMARK('UNRECOGNIZED MESSAGE TYPE '//MTYPE)
        RC = -1
      ENDIF
      END
      SUBROUTINE NJMSEND(HDR,ACN,RC,RECIP,NODE,TEXT)
C
C     NJMSEND - SEND A MESSAGE TO A REMOTE USER OR SERVICE
C
C       HDR   - (IN)  NETWORK COMMUNICATION HEADER ARRAY
C       ACN   - (IN)  APPLICATION CONNECTION NUMBER ON WHICH TO SEND
C       RC    - (OUT) RETURN CODE (0 INDICATES SUCCESS)
C       RECIP - (IN)  NAME OF DESTINATION USER OR SERVICE
C       NODE  - (IN)  NAME OF DESTINATION NJE NODE
C       TEXT  - (IN)  THE TEXT TO SEND
C
C$    COLLATE(FIXED)
      IMPLICIT INTEGER (A-Z)
      DIMENSION HDR(10),TA(20)
      CHARACTER NODE*(*),RECIP*(*),TEXT*(*)
      COMMON /NJM/ CURUSER
      DATA MSK /O"77777777777777007777"/

      IF (CURUSER.EQ.0) THEN
        CURUSER = NJMGETU()
      ENDIF

      TA(1) = L"M".OR.(SHIFT(CURUSER,54).AND.-MASK(6))

      TA(2) = 0
      L = LEN(RECIP)
      IF (L.GT.8) L = 8
      DO 10 I = 1,L
        TA(2) = SHIFT(TA(2),6).OR.ICHAR(RECIP(I:I))
 10   CONTINUE
 11   CONTINUE
      IF (L.LT.8) THEN
        TA(2) = SHIFT(TA(2),6).OR.R" "
        L = L + 1
        GOTO 11
      ENDIF
      TA(2) = SHIFT(TA(2),12)

      TA(3) = 0
      L = LEN(NODE)
      IF (L.GT.8) L = 8
      DO 20 I = 1,L
        TA(3) = SHIFT(TA(3),6).OR.ICHAR(NODE(I:I))
 20   CONTINUE
 21   CONTINUE
      IF (L.LT.8) THEN
        TA(3) = SHIFT(TA(3),6).OR.R" "
        L = L + 1
        GOTO 21
      ENDIF
      L = LEN(TEXT)
      TA(3) = SHIFT(TA(3),12).OR.L

      CI  = 1
      TAI = 3
      I   = 0
 30   CONTINUE
      IF (CI.LE.L) THEN
        IF (I.EQ.0) THEN
          TAI = TAI + 1
          TA(TAI) = 0
        ENDIF
        TA(TAI) = SHIFT(TA(TAI),6).OR.ICHAR(TEXT(CI:CI))
        CI = CI + 1
        I  =  I + 1
        IF (I.GE.10) I = 0
        GOTO 30
      ENDIF
      IF (I.LT.10) THEN
        TA(TAI) = SHIFT(TA(TAI),6 * (10 - I))
      ENDIF

      HDR(1) = (HDR(1).AND.MSK).OR.SHIFT(1,12)
      HDR(5) = SHIFT(400,48).OR.SHIFT(TAI,36).OR.SHIFT(ACN,18)
      CALL QTPUT(TA)
      CALL NJMSTAT(HDR,IGN,RC)

      END
      SUBROUTINE NJMSETU(HDR,ACN,RC,USER)
C
C     NJMSETU - SET USER ID ASSOCIATED WITH CONNECTION
C
C       HDR   - (IN)  NETWORK COMMUNICATION HEADER ARRAY
C       ACN   - (IN)  APPLICATION CONNECTION NUMBER
C       RC    - (OUT) RETURN CODE (0 INDICATES SUCCESS)
C       USER  - (IN)  THE USER ID TO ASSOCIATE
C
C$    COLLATE(FIXED)
      IMPLICIT INTEGER (A-Z)
      DIMENSION HDR(10),TA(2)
      CHARACTER USER*(*)
      COMMON /NJM/ CURUSER
      DATA CURUSER /0/
      DATA MSK /O"77777777777777007777"/

      IF (USER.EQ.'*') THEN
        CURUSER = NJMGETU()
      ELSE
        CURUSER = 0
        L = LEN(USER)
        IF (L.GT.8) L = 8
        DO 10 I = 1,L
          CURUSER = SHIFT(CURUSER,6).OR.ICHAR(USER(I:I))
 10     CONTINUE
 11     CONTINUE
        IF (L.LT.10) THEN
          CURUSER = SHIFT(CURUSER,6).OR.R" "
          L = L + 1
          GOTO 11
        ENDIF
      ENDIF
      TA(1)   = L"I".OR.(SHIFT(CURUSER,54).AND.-MASK(6))
      TA(2)   = 0
      HDR(1)  = (HDR(1).AND.MSK).OR.SHIFT(1,12)
      HDR(5)  = SHIFT(400,48).OR.SHIFT(2,36).OR.SHIFT(ACN,18)
      CALL QTPUT(TA)
      CALL NJMSTAT(HDR,IGN,RC)

      END
      SUBROUTINE NJMSTAT(HDR,ACN,RC)
C
C     NJMSTAT - EXTRACT CONNECTION NuMBER AND RETURN CODE FROM
C               NETWORK COMMUNICATION HEADER
C
C       HDR   - (IN)  NETWORK COMMUNICATION HEADER ARRAY
C       ACN   - (OUT) APPLICATION CONNECTION NUMBER
C       RC    - (OUT) RETURN CODE
C
      IMPLICIT INTEGER (A-Z)
      DIMENSION HDR(10)

      RC  = SHIFT(HDR(5),-12).AND.O"77"
      ACN = SHIFT(HDR(5),-18).AND.O"7777"

      END
          IDENT  NJMABT
          ENTRY  NJMABT
 NJMABT   BSS    1
          ABORT
          END
          IDENT  NJMGETU
          ENTRY  NJMGETU
*CALL COMCSFN
 NJMGETU  BSS    1
          SB1    1
          USERNUM UN
          SA1    UN
          RJ     SFN
          EQ     NJMGETU

 UN       BSS    1

          END
