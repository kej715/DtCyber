.PROC,AUDIT*I"LIST PERMANENT FILES",
DC"DISPOSITION CODE"=(*F,*N=TO).
$CSUBMIT,CRAYJOB,DC.
$REVERT,NOLIST.
.DATA,CRAYJOB.
JOB,JN=AUDIT.
ACCOUNT,US=SYSTEM,AC=CRAY,APW=XYZZY,UPW=QUASAR.
AUDIT.
~eor
.PROC,CAL*I"ASSEMBLE SOURCE FILE",
I"SOURCE FILE NAME"=(*F,*N=INPUT),
DC"DISPOSITION CODE"=(*F,*N=TO),
UN"RBF TERMINAL USERNAME"=(*F,*N=).
.IF,FILE(I,AS),LOCAL.
  .IF,$I$.NE.$INPUT$,NOTINP.
    $REWIND,I.
  .ENDIF,NOTINP.
.ELSE,LOCAL.
  $GET,I. 
.ENDIF,LOCAL.
$SKIPEI,CRAYJOB.
$COPY,I,CRAYJOB.
$FCOPY,P=CRAYJOB,N=ZZZZASC,NC=ASCII8,R. 
.IF,$UN$.NE.$$,UN.
  $CSUBMIT,ZZZZASC,RB,RUN=UN. 
.ELSE,UN. 
  $CSUBMIT,ZZZZASC,DC.
.ENDIF,UN.
$REVERT,NOLIST.
.DATA,CRAYJOB.
JOB,JN=CAL,T=60.
ACCOUNT,US=SYSTEM,AC=CRAY,APW=XYZZY,UPW=QUASAR.
ECHO,ON=ALL.
OPTION,STAT=ON.
COPYF,O=I.
REWIND,DN=I.
CAL,#I=I. 
LDR,M=$OUT,AB,DN=$BLD.
$ABD.
~eor
.PROC,DELETE*I"DELETE PERMANENT FILE",
F"PERMANENT FILE NAME"=(*F),
ED"EDITION"=(*N=),
DC"DISPOSITION CODE"=(*F,*N=TO).
$CSUBMIT,CRAYJOB,DC.
$REVERT,NOLIST.
.DATA,CRAYJOB.
JOB,JN=DELETE.
ACCOUNT,US=SYSTEM,AC=CRAY,APW=XYZZY,UPW=QUASAR.
ECHO,ON=ALL.
OPTION,STAT=ON.
.IF,$ED$.NE.$$,HASED.
ACCESS,DN=F,#ED=ED,UQ.
.ELSE,HASED.
ACCESS,DN=F,UQ.
.ENDIF,HASED.
DELETE,DN=F.
~eor
.PROC,FTN*I"COMPILE AND RUN FORTRAN PROGRAM",
I"SOURCE FILE NAME"=(*F,*N=INPUT),
DC"DISPOSITION CODE"=(*F,*N=TO),
UN"RBF TERMINAL USERNAME"=(*F,*N=),
T"CPU TIME LIMIT"=(*S/D,*N=180),
D"DATA INPUT FILE"=(*F,*N=),
DN"DATASET NAME FOR INPUT FILE"=(*F,*N=).
.IF,FILE(I,AS),LOCAL.
  .IF,$I$.NE.$INPUT$,NOTINP.
    $REWIND,I.
  .ENDIF,NOTINP.
.ELSE,LOCAL.
  $GET,I. 
.ENDIF,LOCAL.
$SKIPEI,CRAYJOB.
$COPY,I,CRAYJOB.
.IF,$D$.NE.$$,DATA.
  .IF,FILE(D,AS),LCLDATA.
    $REWIND,D.
  .ELSE,LCLDATA.
    $GET,D.
  .ENDIF,LCLDATA.
  $COPY,D,CRAYJOB.
.ENDIF,DATA.
$FCOPY,P=CRAYJOB,N=ZZZZASC,NC=ASCII8,R. 
.IF,$UN$.NE.$$,UN.
  $CSUBMIT,ZZZZASC,RB,RUN=UN. 
.ELSE,UN. 
  $CSUBMIT,ZZZZASC,DC.
.ENDIF,UN.
$REVERT,NOLIST.
.DATA,CRAYJOB.
JOB,JN=FTN,#T=T.
ACCOUNT,US=SYSTEM,AC=CRAY,APW=XYZZY,UPW=QUASAR.
ECHO,ON=ALL.
OPTION,STAT=ON.
ACCESS,#DN=PROCLIB,OWN=SYSTEM. 
LIBRARY,#DN=*:PROCLIB.
COPYF,O=I.
REWIND,#DN=I.
.IF,$DN$.NE.$$,NAMED.
COPYF,O=DN.
REWIND,#DN=DN.
.ENDIF,NAMED.
MEMORY,FL,USER.
FTN,#I=I,GO.
~eor
.PROC,INSTALL*I"INSTALL AN EXECUTABLE AS A COMMAND",
F"EXECUTABLE FILE NAME"=(*F), 
EXT"RESIDENT ON EXTENDER DISK"=(*F,*N=NO),
DC"DISPOSITION CODE"=(*F,*N=TO).
.IF,$EXT$.EQ.$NO$,EXTDSK1.
  .IF,FILE(F,AS),LOCAL.
    $REWIND,F.
  .ELSE,LOCAL.
    $GET,F.
  .ENDIF,LOCAL.
  $COPYBF,F,ZZZCBIN.
  $REPLACE,ZZZCBIN=F.
.ENDIF,EXTDSK1.
$CSUBMIT,CRAYJOB,DC.
$REVERT,NOLIST.
.DATA,CRAYJOB.
JOB,JN=INSTALL.
ACCOUNT,US=SYSTEM,AC=CRAY,APW=XYZZY,UPW=QUASAR.
ECHO,ON=ALL.
OPTION,STAT=ON.
.IF,$EXT$.EQ.$NO$,EXTDSK2.
FETCH,DN=F,MF=FE,DF=TR,^
TEXT='USER,INSTALL,${../site.cfg:PASSWORDS:INSTALL:INSTALL}.GET,F.CTASK.'.
.ELSE,EXTDSK2.
FETCH,DN=F,MF=AP,AC=ST,TEXT=BIN/F.
.ENDIF,EXTDSK2.
SAVE,DN=F,EXO=ON,RT=4095.
RELEASE,DN=F.
ACCESS,DN=F,ENTER.
~eor
.PROC,LISP*I"RUN LISP PROGRAM",
I"SOURCE FILE NAME"=(*F,*N=INPUT),
DC"DISPOSITION CODE"=(*F,*N=TO),
UN"RBF TERMINAL USERNAME"=(*F,*N=),
T"CPU TIME LIMIT"=(*S/D,*N=180),
D"DATA INPUT FILE"=(*F,*N=).
.IF,FILE(I,AS),LOCAL.
  .IF,$I$.NE.$INPUT$,NOTINP.
    $REWIND,I.
  .ENDIF,NOTINP.
.ELSE,LOCAL.
  $GET,I.
.ENDIF,LOCAL.
$SKIPEI,LISPIN.
$COPY,I,LISPIN.
$PACK,LISPIN.
$REWIND,LISPIN.
$SKIPEI,CRAYJOB.
.IF,$D$.NE.$$,DATA.
  $REWIND,D.
  $COPY,D,CRAYJOB.
.ENDIF,DATA.
$COPY,LISPIN,CRAYJOB.
$FCOPY,P=CRAYJOB,N=ZZZZASC,NC=ASCII8,R.
.IF,$UN$.NE.$$,UN.
  $CSUBMIT,ZZZZASC,RB,RUN=UN.
.ELSE,UN.
  $CSUBMIT,ZZZZASC,DC.
.ENDIF,UN.
$REVERT,NOLIST.
.DATA,CRAYJOB.
JOB,JN=LISP,#T=T.
ACCOUNT,US=SYSTEM,AC=CRAY,APW=XYZZY,UPW=QUASAR.
ECHO,ON=ALL.
OPTION,STAT=ON.
ACCESS,DN=PROCLIB,OWN=SYSTEM.
LIBRARY,DN=*:PROCLIB.
.IF,$D$.NE.$$,DATAF.
COPYF,O=D.
REWIND,DN=D.
.ENDIF,DATAF.
MEMORY,FL,USER.
LISP.
.DATA,LISPIN.
(ROLLIN (OPEN0 'LISPINI T T))
~eor
.PROC,QGET*I"RETRIEVE FILE FROM WAIT QUEUE",
JSN"JOB SEQUENCE NAME"=(*F,*N=),
UJN"USER JOB NAME"=(*F,*N=),
CS"CHARACTER SET"=(*F,*N=ASCII8).
.IF,$JSN$.NE.$$,DOJSN.
  $QGET,JSN.
  $FCOPY,P=JSN,PC=CS,N=ZZZZTMP,NC=ASCII.
  $RENAME,JSN=ZZZZTMP.
  $REWIND,JSN.
.ELSEIF,$UJN$.NE.$$,DOJSN.
  $QGET,i#UJN=UJN.
  $FCOPY,P=UJN,PC=CS,N=ZZZZTMP,NC=ASCII.
  $RENAME,UJN=ZZZZTMP.
  $REWIND,UJN.
.ENDIF,DOJSN.
$REVERT,NOLIST.
~eor
.PROC,REPLACE*I"REPLACE A PERMANENT FILE",
F"NAME OF NOS FILE"=(*F),
N"NAME OF COS PERMANENT FILE"=(*N=),
PAM"PUBLIC ACCESS MODE"=(*F,*N=R),
DF"DATA FORMAT"=(*F,*N=TR),
DC"DISPOSITION CODE"=(*F,*N=TO).
.IF,FILE(F,AS),LOCAL.
  $REWIND,F.
.ELSE,LOCAL.
  $GET,F. 
.ENDIF,LOCAL.
$COPYBF,F,ZZZCBIN.
$REPLACE,ZZZCBIN=F. 
$CSUBMIT,CRAYJOB,DC.
$REVERT,NOLIST.
.DATA,CRAYJOB.
JOB,JN=REPLACE.
ACCOUNT,US=SYSTEM,AC=CRAY,APW=XYZZY,UPW=QUASAR.
ECHO,ON=ALL.
OPTION,STAT=ON.
ACCESS,DN=F,UQ,NA.
DELETE,DN=F,NA.
RELEASE,DN=F.
FETCH,DN=F,MF=FE,#DF=DF,^
TEXT='USER,INSTALL,${../site.cfg:PASSWORDS:INSTALL:INSTALL}.GET,F.CTASK.'.
.IF,$N$.NE.$$,HASN. 
SAVE,DN=F,PDN=N,#PAM=PAM,RT=4095.
.ELSE,HASN.
SAVE,DN=F,#PAM=PAM,RT=4095.
.ENDIF,HASN.
~eor
.PROC,RUN*I"TRANSFER AND RUN A PROGRAM",
B"NAME OF EXECUTABLE FILE"=(*F,*N=CRAYBIN),
I"NAME OF OPTIONAL DATA FILE"=(*F,*N=), 
DF"DATA FORMAT OF DATA FILE"=(*F,*N=CB),
T"CPU TIME LIMIT"=(*S/D,*N=10),
DC"DISPOSITION CODE"=(*F,*N=TO).
.IF,FILE(B,AS),LOCAL.
  $REWIND,B.
.ELSE,LOCAL.
  $GET,B. 
.ENDIF,LOCAL.
$COPYBF,B,ZZZCBIN.
$REPLACE,ZZZCBIN=B. 
.IF,$I$.NE.$$.AND.$DF$.EQ.$CB$,DF1.
  .IF,FILE(I,AS),DFLOCAL.
    $REWIND,I.
  .ELSE,DFLOCAL.
    $GET,I.
  .ENDIF,DFLOCAL.
  $SKIPEI,CRAYJOB.
  $COPY,I,CRAYJOB.
.ENDIF,DF1.
$FCOPY,P=CRAYJOB,PC=ASCII,N=ZZZZASC,NC=ASCII8,R.
$CSUBMIT,ZZZZASC,DC.
$REVERT,NOLIST.
.DATA,CRAYJOB.
JOB,JN=RUN,#T=T.
ACCOUNT,US=SYSTEM,AC=CRAY,APW=XYZZY,UPW=QUASAR.
ECHO,ON=ALL.
OPTION,STAT=ON.
.IF,$I$.NE.$$.AND.$DF$.EQ.$CB$,DF2.
  COPYF,O=I.
  REWIND,DN=I.
.ENDIF,DF2.
FETCH,DN=B,MF=FE,#DF=TR,@#B
TEXT='USER,INSTALL,${../site.cfg:PASSWORDS:INSTALL:INSTALL}.GET,B.CTASK.'.
.IF,$I$.NE.$$.AND.$DF$.EQ.$TR$,FTCH.
FETCH,DN=I,MF=FE,#DF=DF,@#B
TEXT='USER,INSTALL,${../site.cfg:PASSWORDS:INSTALL:INSTALL}.GET,I.CTASK.'.
.ENDIF,FTCH.
MEMORY,FL,USER.
.IF,$I$.EQ.$$,NODATA.
B.
.ELSE,NODATA.
B,I.
.ENDIF,NODATA.
~eor
.PROC,SAVE*I"SAVE A PERMANENT FILE",
F"NAME OF NOS FILE"=(*F),
N"NAME OF COS PERMANENT FILE"=(*N=),
PAM"PUBLIC ACCESS MODE"=(*F,*N=R),
DF"DATA FORMAT"=(*F,*N=TR),
DC"DISPOSITION CODE"=(*F,*N=TO).
.IF,FILE(F,AS),LOCAL.
  $REWIND,F.
.ELSE,LOCAL.
  $GET,F. 
.ENDIF,LOCAL.
$COPYBF,F,ZZZCBIN.
$REPLACE,ZZZCBIN=F. 
$CSUBMIT,CRAYJOB,DC.
$REVERT,NOLIST.
.DATA,CRAYJOB.
JOB,JN=SAVE.
ACCOUNT,US=SYSTEM,AC=CRAY,APW=XYZZY,UPW=QUASAR.
ECHO,ON=ALL.
OPTION,STAT=ON.
FETCH,DN=F,MF=FE,#DF=DF,^
TEXT='USER,INSTALL,${../site.cfg:PASSWORDS:INSTALL:INSTALL}.GET,F.CTASK.'.
.IF,$N$.NE.$$,HASN. 
SAVE,DN=F,PDN=N,#PAM=PAM,RT=4095.
.ELSE,HASN.
SAVE,DN=F,#PAM=PAM,RT=4095.
.ENDIF,HASN.
