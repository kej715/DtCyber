      PROGRAM NJMSG
C
C     NJMSG - SEND A MESSAGE AND WAIT FOR A RESPONSE
C
C     COMMAND LINE PARAMETERS
C       I - NAME OF LOCAL FILE CONTAINING TEXT TO SEND
C       N - NAME OF DESTINATION NODE
C       O - NAME OF LOCAL FILE TO WHICH TO WRITE OUTPUT
C       U - NAME OF DESTINATION USER OR SERVICE
C       W - MAXIMUM SECONDS TO WAIT FOR RESPONSE (DEFAULT 5)
C
C     TEXT TO BE SENT FOLLOWS COMMAND TERMINATOR.
C
      IMPLICIT INTEGER (A-Z)
      DIMENSION HDR(10),NIT(10,1),TA(20)
      CHARACTER*1   CH,MTYPE
      CHARACTER*8   DNODE,DUSER,IFNAME,OFNAME,ONODE,OUSER
      CHARACTER*10  ARG,ARGV(20)
      CHARACTER*150 TEXT

      DUSER  = ' '
      DNODE  = ' '
      IFNAME = ' '
      MSGC   = 0
      OFNAME = 'OUTPUT'
      WAIT   = 5

      CALL NJMARGS(ARGC,ARGV,TEXT,TEXTL)

      DO 10 I = 2,ARGC
        ARG = ARGV(I)
        IF (ARG(1:2).EQ.'I=') THEN
          IFNAME = ARG(3:10)
        ELSEIF (ARG(1:2).EQ.'N=') THEN
          DNODE = ARG(3:10)
        ELSEIF (ARG(1:2).EQ.'O=') THEN
          OFNAME = ARG(3:10)
        ELSEIF (ARG(1:2).EQ.'U=') THEN
          DUSER = ARG(3:10)
        ELSEIF (ARG(1:2).EQ.'W=') THEN
          READ(ARG(3:10),'(I8)') WAIT
        ELSE
          CALL REMARK('INVALID PARAMETER '//ARG(1:2))
          CALL NJMABT
        ENDIF
 10   CONTINUE

      IF (DUSER.EQ.' ') THEN
        CALL REMARK('MISSING PARAMETER U=')
        CALL NJMABT
      ENDIF
      IF (DNODE.EQ.' ') THEN
        CALL REMARK('MISSING PARAMETER N=')
        CALL NJMABT
      ENDIF

      OPEN(1,FILE=OFNAME,STATUS='UNKNOWN')

      IF (IFNAME.NE.' ') THEN
        OPEN(2,FILE=IFNAME,STATUS='OLD')
        READ(2,'(A)') TEXT
        TEXTL = NBLEN(TEXT)
      ENDIF

      CALL NJMOPEN(HDR,RC,"NJU",1)
      IF (RC.NE.0) THEN
        CALL DISPLA('FAILED TO OPEN COMMUNICATION, RC =',RC)
        CALL NJMABT
      ENDIF
      CALL NJMCONN(HDR,ACN,RC)
      IF (RC.NE.0) THEN
        CALL DISPLA('FAILED TO CONNECT TO NJF, RC =',RC)
        CALL NJMABT
      ENDIF

      IF (TEXTL.GT.0) THEN
        CALL NJMSEND(HDR,ACN,RC,'*',DUSER,DNODE,TEXT(1:TEXTL))
        IF (RC.NE.0) THEN
          CALL DISPLA('FAILED TO SEND MESSAGE, RC =',RC)
          CALL NJMABT
        ENDIF
        MSGC = MSGC + 1
      ELSE
        CALL NJMSETU(HDR,ACN,RC,'*')
        IF (RC.NE.0) THEN
          CALL DISPLA('FAILED TO SET USERNAME, RC =',RC)
          CALL NJMABT
        ENDIF
      ENDIF

      DLINE = NJMSECS() + WAIT
 20   CONTINUE
      T = NJMSECS()
      IF (DLINE.GT.T) THEN
        SLEEP = DLINE - T
        IF (SLEEP.LE.0) THEN
          SLEEP = 1
        ELSEIF (SLEEP.GT.30) THEN
          SLEEP = 30
        ENDIF
        CALL NJMPOLL(HDR,CN,RC,SLEEP,TA,20)
        IF (RC.EQ.0) THEN
          CALL NJMRECV(TA,MTYPE,OUSER,ONODE,DUSER,TEXT,TEXTL,RC)
          IF (MTYPE.EQ.'S') THEN
            IF (RC.EQ.2) THEN
              CALL REMARK('NJE RESOURCES EXHAUSTED')
              CALL NJMABT
            ELSEIF (RC.NE.0) THEN
              CALL DISPLA('REQUEST FAILED, RC =',RC)
              CALL NJMABT
            ENDIF
          ELSEIF (MTYPE.EQ.'M') THEN
            WRITE(1,21) OUSER(1:NBLEN(OUSER)),
     +                  ONODE(1:NBLEN(ONODE)),
     +                  DUSER(1:NBLEN(DUSER)),
     +                  TEXTL
 21         FORMAT(' FROM ',A,' AT ',A,' TO ',A,' LEN ',I3)
            WRITE(1,'(1X,A)') TEXT(1:TEXTL)
          ENDIF
          DLINE = NJMSECS() + WAIT
          GO TO 20
        ELSE IF (RC.EQ.1) THEN
 30       CONTINUE
          IF (IFNAME.NE.' ') THEN
            IF ((NIT(4,ACN).AND.-MASK(54)).GT.0) THEN
              READ(2,'(A)',END=40) TEXT
              TEXTL = NBLEN(TEXT)
              CALL NJMSEND(HDR,ACN,RC,'*',DUSER,DNODE,TEXT(1:TEXTL))
              IF (RC.NE.0) THEN
                CALL DISPLA('FAILED TO SEND MESSAGE, RC =',RC)
                CALL NJMABT
              ENDIF
              MSGC = MSGC + 1
              IF (MOD(MSGC,8).EQ.0) THEN
                CALL DELAY(1)
                DLINE  = NJMSECS() + WAIT
                GOTO 20
              ENDIF
              GOTO 30
 40           CONTINUE
              IFNAME = ' '
            ENDIF
            DLINE  = NJMSECS() + WAIT
          ENDIF
          GO TO 20
        ELSE IF (RC.EQ.6) THEN
          CALL REMARK('CONNECTION ENDED')
          GO TO 50
        ELSE IF (RC.EQ.13) THEN
          CALL REMARK('CONNECTION REJECTED')
          GO TO 50
        ELSE
          CALL DISPLA('UNRECOGNIZED STATUS ', RC)
          CALL NJMABT
        ENDIF
      ENDIF

 50   CONTINUE
      CALL NJMCLOS

      END
      FUNCTION NBLEN(STR)
      IMPLICIT INTEGER (A-Z)
      CHARACTER STR*(*)

      L = LEN(STR)
 10   CONTINUE
      IF (L.GT.0) THEN
        IF (STR(L:L).EQ.' ') THEN
          L = L - 1
          GOTO 10
        ENDIF
      ENDIF

      IF (L.LT.1.AND.LEN(STR).GT.0) L = 1
      NBLEN = L

      END
          IDENT  ARG
          ENTRY  ARG=
 ARG=     BSS    0
          END
          IDENT  DELAY
          ENTRY  DELAY

 DELAY    BSS    1
          SA3    DELA
          SA2    X1
          MX0    48
          BX6    X0*X3
          BX6    X6+X2
          SA6    A3
          ROLLOUT DELA
          EQ      DELAY

 DELA     VFD     27/0,21/20000B,12/0

          END
