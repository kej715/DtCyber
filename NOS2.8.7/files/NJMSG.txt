      PROGRAM NJMSG
C
C     NJMSG - SEND A MESSAGE AND WAIT FOR A RESPONSE
C
C     COMMAND LINE PARAMETERS
C       N - NAME OF DESTINATION NODE
C       U - NAME OF DESTINATION USER OR SERVICE
C       W - MAXIMUM SECONDS TO WAIT FOR RESPONSE (DEFAULT 5)
C
C     TEXT TO BE SENT FOLLOWS COMMAND TERMINATOR.
C
      IMPLICIT INTEGER (A-Z)
      DIMENSION HDR(10),NIT(10,12),TA(20)
      CHARACTER*1   MTYPE
      CHARACTER*8   OUSER,ONODE,DUSER,DNODE
      CHARACTER*10  ARG,ARGV(20)
      CHARACTER*150 TEXT

      DUSER = ' '
      DNODE = ' '
      WAIT  = 5

      CALL NJMARGS(ARGC,ARGV,TEXT,TEXTL)

      DO 10 I = 2,ARGC
        ARG = ARGV(I)
        IF (ARG(1:2).EQ.'N=') THEN
          DNODE = ARG(3:10)
        ELSEIF (ARG(1:2).EQ.'U=') THEN
          DUSER = ARG(3:10)
        ELSEIF (ARG(1:2).EQ.'W=') THEN
          READ(ARG(3:10),'(I8)') WAIT
        ELSE
          CALL REMARK('INVALID PARAMETER '//ARG(1:2))
          CALL NJMABT
        ENDIF
 10   CONTINUE

      IF (DUSER.EQ.' ') THEN
        CALL REMARK('MISSING PARAMETER U=')
        CALL NJMABT
      ENDIF
      IF (DNODE.EQ.' ') THEN
        CALL REMARK('MISSING PARAMETER N=')
        CALL NJMABT
      ENDIF
      IF (TEXTL.LT.1) THEN
        CALL REMARK('NO MESSAGE TEXT PROVIDED')
        CALL NJMABT
      ENDIF

      CALL NJMOPEN(HDR,RC,"AP1",1)
      IF (RC.NE.0) THEN
        CALL DISPLA('FAILED TO OPEN COMMUNICATION, RC =',RC)
        CALL NJMABT
      ENDIF
      CALL NJMCONN(HDR,ACN,RC)
      IF (RC.NE.0) THEN
        CALL DISPLA('FAILED TO CONNECT TO NJF, RC =',RC)
        CALL NJMABT
      ENDIF
      CALL NJMSEND(HDR,ACN,RC,'*',DUSER,DNODE,TEXT(1:TEXTL))
      IF (RC.NE.0) THEN
        CALL DISPLA('FAILED TO SEND MESSAGE, RC =',RC)
        CALL NJMABT
      ENDIF
      T     = SHIFT(NJMTIME(),-36).AND.O"77777777"
      DLINE = T + WAIT
 20   CONTINUE
      T = SHIFT(NJMTIME(),-36).AND.O"77777777"
      CALL NJMPOLL(HDR,CN,RC,DLINE - T,TA,20)
      IF (RC.EQ.0) THEN
        CALL NJMRECV(TA,MTYPE,OUSER,ONODE,DUSER,TEXT,TEXTL,RC)
        IF (MTYPE.EQ.'S') THEN
          IF (RC.NE.0) THEN
            CALL DISPLA('REQUEST FAILED, RC =',RC)
            CALL NJMABT
          ENDIF
        ELSEIF (MTYPE.EQ.'M') THEN
          PRINT 21,OUSER,ONODE,DUSER,TEXTL
 21       FORMAT(' FROM ',A,' AT ',A,' TO ',A,' LEN ',I3)
          PRINT '(1X,A)',TEXT(1:TEXTL)
        ENDIF
        T     = SHIFT(NJMTIME(),-36).AND.O"77777777"
        DLINE = T + WAIT
        GO TO 20
      ELSE IF (RC.EQ.1) THEN
        T = SHIFT(NJMTIME(),-36).AND.O"77777777"
        IF (T.GE.DLINE) GOTO 30
        GO TO 20
      ELSE IF (RC.EQ.6) THEN
        CALL REMARK('CONNECTION ENDED')
        GO TO 30
      ELSE IF (RC.EQ.13) THEN
        CALL REMARK('CONNECTION REJECTED')
        GO TO 30
      ELSE
        CALL DISPLA('UNRECOGNIZED STATUS ', RC)
        CALL NJMABT
      ENDIF

 30   CONTINUE

      END
          IDENT  ARG
          ENTRY  ARG=
 ARG=     BSS    0
          END
