~*
~*  This job builds the GPLOT product. It builds the executables and
~*  libraries, edits the GPLOT CCL procedure into the PRODUCT file,
~*  and inserts a directive into the DIRFILE file specifying into which
~*  deadstart file library the GPLOT procedure will be inserted.
~*
GPLOT.
$USER,INSTALL,INSTALL.
$NORERUN.
$SETTL,*.
$SETASL,*.
$SETJSL,*.
$COPYBR,INPUT,DIR.
$COPYBR,INPUT,GPROC.
$ASSIGN,${2},TAPE,D=GE,LB=KU,F=I,PO=R.
$SKIPF,TAPE.
$COPYBF,TAPE,PLDIMFM.
$COPYBF,TAPE,PLGRDEV.
$COPYBF,TAPE,PLUTILS.
$COPYBF,TAPE,FTGPLOT.
*
*  COPY FONT FILE FROM TAPE
*
$PURGE,DADIMFO/NA.
$DEFINE,DADIMFO.
$COPYBF,TAPE,DADIMFO.
*
*  BUILD DIMFILM OBJECT LIBRARY
*
$REWIND,PLDIMFM.
$RETURN,LGO,COMPILE.
$MODIFY,P=PLDIMFM,F,LO=E,I=0.
$FTN5,I,L=LIST,OPT.
$LIBGEN,P=LBDIMFM.
*
*  BUILD GRAPHICS DEVICE OBJECT LIBRARY
*
$REWIND,PLGRDEV.
$RETURN,LGO,COMPILE.
$MODIFY,P=PLGRDEV,F,LO=E,I=0.
$FTN5,I,L=LIST,OPT.
$LIBGEN,P=LBGRDEV.
*
*  BUILD FORTRAN UTILITY ROUTINE LIBRARY
*
$REWIND,PLUTILS.
$RETURN,LGO,COMPILE.
$MODIFY,P=PLUTILS,F,LO=E,I=0.
$FTN5,I,L=LIST,OPT.
$LIBGEN,P=LBUTILS.
*
*  BUILD THE GPLOT PROGRAM.
*
$REWIND,FTGPLOT.
$RETURN,LGO.
$FTN5,I=FTGPLOT,L=LIST,OPT,PL=9999999.
$LDSET,LIB=LBDIMFM/LBGRDEV/LBUTILS/SYMLIB.
$LDSET,USEP=DFXB00.
$LDSET,USEP=EPSBDB.
$LDSET,PRESET=ZERO.
$LOAD,LGO.
$NOGO,GPLOT.
*
*  SAVE THE LIBRARIES AND GPLOT APPLICATION
*
$PURGE,GPLOT,LBDIMFM,LBGRDEV,LBUTILS/NA.
$REWIND,GPLOT,LBDIMFM,LBGRDEV,LBUTILS.
$DEFINE,F=GPLOT.
$COPY,GPLOT,F.
$RETURN,F.
$DEFINE,F=LBDIMFM.
$COPY,LBDIMFM,F.
$RETURN,F.
$DEFINE,F=LBGRDEV.
$COPY,LBGRDEV,F.
$RETURN,F.
$DEFINE,F=LBUTILS.
$COPY,LBUTILS,F.
$REWIND,GPROC,DIR.
$ATTACH,PRODUCT,DIRFILE/M=W,WB.
$LIBEDIT,P=PRODUCT,B=GPROC,I=0,C.
$LIBEDIT,P=DIRFILE,B=DIR,I=0,C.
***
*** GPLOT COMPLETE
***
EXIT.
***
*** GPLOT FAILED
***
~eor
GPLOT
*ADD LIB${1},PROC/GPLOT
~eor
.PROC,GPLOT*I,OBEY=(*F,*N=),PARM=(*A,*N=).
$ATTACH,ZZZGPLT=GPLOT/UN=LIBRARY.
$IF,$OBEY$.NE.$$,HASOBEY.
$  IF,$PARM$.NE.$$,HASPARM.
     ZZZGPLT,#OBEY=OBEY,#PARM=$PARM$.
$  ELSE,HASPARM.
     ZZZGPLT,#OBEY=OBEY.
$  ENDIF,HASPARM.
$ELSE,HASOBEY.
  ZZZGPLT.
$ENDIF,HASOBEY.
$RETURN,ZZZGPLT.
$REVERT,NOLIST.
