~*
~*  This job saves a CCL procedure that will be run by
~*  DIS to create and initialize the MAILDB fast attach file
~*  and arrange for it to be added to the system's fast
~*  attach table at deadstart time.
~*
MLRDB.
$USER,INSTALL,INSTALL.
$NORERUN.
$SETTL,*.
$SETASL,*.
$SETJSL,*.
$COPY,INPUT,MLRCRDB.
$REPLACE,MLRCRDB.
***
*** MLRDB COMPLETE
***
EXIT.
***
*** MLRDB FAILED
***
~eor
.PROC,MLRCRDB.
$CATLIST,FN=MAILDB.
$SKIP,EXISTS.
$EXIT.
$  DEFINE,MAILDB.
$  WRITER,MAILDB.
$  RETURN,MAILDB.
$ENDIF,EXISTS.
$NOEXIT.
$IMF,ADD.
$ONEXIT.
$DBINIT,CF=MLRCONF.
$GET,SYSPROC/NA.
$IF,FILE(SYSPROC,AS),UPDSYSP.
$  XEDIT,SYSPROC,I=0,L=0.D/IMF,ADD./*;Q
$  SKIPEI,SYSPROC.
$  NOTE,SYSPROC,NR./$IMF,ADD.
$  PACK,SYSPROC.
$  REPLACE,SYSPROC.
$ELSE,UPDSYSP.
$  NOTE,SYSPROC./.PROC,SYSPROC./$IMF,ADD.
$  SAVE,SYSPROC.
$ENDIF,UPDSYSP.
$REVERT.
.DATA,MLRCONF.
${1}
${2}
