~*
~*  This job installs COS tools on a connected Cray X-MP
~*  system running COS.
~*
COSTOOL.
$USER,INSTALL,${../site.cfg:PASSWORDS:INSTALL:INSTALL}.
$NORERUN.
$SETTL,*.
$SETASL,*.
$SETJSL,*.
$NOEXIT.
$TMSDEF,TO=F.
$ONEXIT.
$COPY,INPUT,CRAYJOB.
$PURGE,COSTLDB/NA.
$RECLAIM,DB=COSTLDB,Z./LIST,TN=COSTLS
$RECLAIM,DB=COSTLDB,Z./LOAD,TN=COSTLS,RP=Y
$CSUBMIT,CRAYJOB. 
~eor
JOB,JN=INSTALL.
ACCOUNT,AC=CRAY,APW=XYZZY,UPW=QUASAR.
ECHO,ON=ALL.
OPTION,STAT=ON.
FETCH,DN=COPYF,MF=FE,DF=TR,^
TEXT='USER,INSTALL,${../site.cfg:PASSWORDS:INSTALL:INSTALL}.GET,COPYF.CTASK.'.
SAVE,DN=COPYF,EXO=ON,RT=4095.
RELEASE,DN=COPYF.
ACCESS,DN=COPYF,ENTER. 
COPYF,O=GENLIB.
CALL,DN=GENLIB.
*INSTEXP,$ACCTDEF.
INSTEXP,AUDIT.
INSTEXP,GENCAT.
INSTEXP,JCSDEF.
INSTEXP,LOADCAT.
INSTEXP,PRVDEF.
INSTEXP,TEDI.
INSTNOS,CAL.
INSTNOS,CHARGES.
INSTNOS,COPYD.
*INSTNOS,COPYF.
INSTNOS,COPYR.
INSTNOS,DASM.
INSTNOS,KFTC.
INSTNOS,LDR.
INSTNOS,LIB.
INSTNOS,NOTE.
INSTNOS,SKIPD.
INSTNOS,SKIPF.
INSTNOS,SKIPR.
SAVELIB,CLIB.
SAVELIB,EMLIB.
SAVELIB,INTFLIB.
SAVELIB,IOLIB.
SAVELIB,RTLIB.
SAVELIB,SYSLIB.
***
*** COSTOOL COMPLETE
***
EXIT.
***
*** COSTOOL FAILED
***
~eor
RELEASE,DN=$PROC.
PROC.
INSTEXP,f.
FETCH,DN=&F,MF=AP,AC=ST,TEXT=BIN/&F.
SAVE,DN=&F,EXO=ON,RT=4095.
RELEASE,DN=&F.
ACCESS,DN=&F,ENTER.
RETURN.
ENDPROC.
PROC.
INSTNOS,F.
FETCH,DN=&F,MF=FE,DF=TR,^
TEXT='USER,INSTALL,${../site.cfg:PASSWORDS:INSTALL:INSTALL}.GET,&F.CTASK.'.
SAVE,DN=&F,EXO=ON,RT=4095.
RELEASE,DN=&F.
ACCESS,DN=&F,ENTER. 
RETURN.
ENDPROC.
PROC.
SAVELIB,F.
FETCH,DN=&F,MF=FE,DF=TR,^
TEXT='USER,INSTALL,${../site.cfg:PASSWORDS:INSTALL:INSTALL}.GET,&F.CTASK.'.
SAVE,DN=&F,PAM=R,RT=4095.
RELEASE,DN=&F.
RETURN.
ENDPROC.
LIBRARY,DN=*:$PROC. 
