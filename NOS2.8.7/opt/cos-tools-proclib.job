~*
~*  This job installs the utility procedure library on the
~*  connected Cray X-MP system.
~*
COSPLIB.
$USER,INSTALL,${../site.cfg:PASSWORDS:INSTALL:INSTALL}.
$NORERUN.
$SETTL,*.
$SETASL,*.
$SETJSL,*.
$COPY,INPUT,CRAYJOB.
$CSUBMIT,CRAYJOB. 
~eor
JOB,JN=INSTALL.
ACCOUNT,AC=CRAY,APW=XYZZY,UPW=QUASAR.
ECHO,ON=ALL.
OPTION,STAT=ON.
COPYF,O=GENLIB.
CALL,DN=GENLIB.
***
*** COSPLIB COMPLETE
***
EXIT.
***
*** COSPLIB FAILED
***
~eor
RELEASE,DN=$PROC.
PROC.
FTN,I=$IN:$IN,L=$OUT,C=ZZZZCAL,B=$BLD,ALLOC=STATIC,AB=$ABD:$ABD,^
  GO='NO':'YES',NOGO='NO':'YES'.
KFTC,I=&I,L=&L,O=&C,ALLOC=&ALLOC.
CAL,X,I=ZZZZCAL,L=0,B=&B.
IF(&GO.EQ.'YES'.OR.&NOGO.EQ.'YES')
  ACCESS,DN=CLIB.
  ACCESS,DN=EMLIB.
  ACCESS,DN=IOLIB.
  ACCESS,DN=INTFLIB.
  ACCESS,DN=RTLIB.
  ACCESS,DN=SYSLIB. 
  LDR,M=MAP,AB=&AB,DN=&B,LIB=IOLIB:INTFLIB:RTLIB:EMLIB:SYSLIB:CLIB.
  IF(&GO.EQ.'YES')
    &AB.
  ENDIF.
ENDIF.
RETURN.
ENDPROC.
ACCESS,DN=PROCLIB,UQ,NA.
SAVE,DN=$PROC,PDN=PROCLIB.
DELETE,DN=PROCLIB,NA.
